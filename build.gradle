import org.asciidoctor.gradle.jvm.AsciidoctorTask
import com.neo4j.gradle.wordpress.WordPressUploadTask

plugins {
    id 'org.asciidoctor.jvm.gems' version '3.2.0' apply false
    id 'org.asciidoctor.jvm.convert' version '3.2.0' apply false
    id "com.neo4j.gradle.wordpress.WordPressPlugin" version "0.0.4"
}

apply plugin: 'org.asciidoctor.jvm.gems'

repositories {
    mavenCentral()
    jcenter()
    ruby {
        gems()
    }
}

dependencies {
    asciidoctorGems 'rubygems:neo4j-asciidoctor-extensions:0.0.5'
}

wordpress {
    username = project.hasProperty('wordpress-username') ? project.property('wordpress-username') : 'neo'
    password = project.hasProperty('wordpress-password') ? project.property('wordpress-password') : ''
    host = project.hasProperty('wordpress-host') ? project.property('wordpress-host') : 'localhost'
    scheme = 'https'
}

def stage = project.findProperty('stage')

group = 'com.neo4j'
version = '1.0.0'
archivesBaseName = 'neo4j-labs-pages'

description = """neo4j-labs-pages"""

asciidoctorj {
    requires "${rootProject.projectDir}/resources/extensions.rb"
    options standalone: false,
            template_dirs: ["${rootProject.projectDir}/resources/templates"]
    attributes 'apoc-version': "3.5.0.4",
            'icons': 'font',
            'stage': stage != null ? stage : '',
            'parent-path': '/labs',
            'document-metadata-attrs-include': 'slug,parent-parent,tags*'
}

task convertHtml(type: AsciidoctorTask) {
    dependsOn asciidoctorGemsPrepare

    baseDir file("${projectDir}/asciidoc")
    sourceDir file("${projectDir}/asciidoc")
    sources {
        exclude 'footer.adoc'
    }
    outputDir file("${projectDir}/build/html")
}

task convert {
    dependsOn convertHtml
}

if (stage == 'production' || stage == 'testing') {
    task wordPressUpload(type: WordPressUploadTask) {
        dependsOn convertHtml
        source = convertHtml.outputs.files
        type = "page"
        status = stage == 'production' ? "publish" : "private"
        template = "page-inner-100-blue.php"
    }
}
